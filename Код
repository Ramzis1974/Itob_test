Процедура ОбработкаПроведения(Отказ, Режим)
	//Регистр Остатки номенклатуры (старая методика)
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	
	//Установка блокировок в регистре Остатки номенклатуры
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Партия", "Партия");
	Блокировка.Заблокировать();	
	
	//Получение данных для движений
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Партия КАК Партия,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК КоличествоДок,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК СуммаДок
		|ПОМЕСТИТЬ ДанныеТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Партия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеТЧ.Номенклатура КАК Номенклатура,
		|	ДанныеТЧ.Партия КАК Партия,
		|	ДанныеТЧ.КоличествоДок КАК КоличествоДок,
		|	ДанныеТЧ.СуммаДок КАК СуммаДок,
		|	ДанныеТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ДанныеТЧ.Номенклатура.ВидНоменклатуры = &ВидНоменклатурыУслуга КАК ЭтоУслуга,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|	ДанныеТЧ КАК ДанныеТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МомВремени,
		|				(Номенклатура, Партия) В
		|					(ВЫБРАТЬ
		|						ДанныеТЧ.Номенклатура КАК Номенклатура,
		|						ДанныеТЧ.Партия КАК Партия
		|					ИЗ
		|						ДанныеТЧ КАК ДанныеТЧ)) КАК ОстаткиНоменклатурыОстатки
		|		ПО ДанныеТЧ.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|			И ДанныеТЧ.Партия = ОстаткиНоменклатурыОстатки.Партия";
	
	Запрос.УстановитьПараметр("ВидНоменклатурыУслуга", Перечисления.ВидыНоменклатуры.Услуга);
	Запрос.УстановитьПараметр("МомВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Себестоимость = 0;
		Если Не ВыборкаДетальныеЗаписи.ЭтоУслуга Тогда //Услуга записывается только в регистр Продажи
			//Проверим хватает ли товара
			Если ВыборкаДетальныеЗаписи.КоличествоДок > ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "По номенклатуре " + ВыборкаДетальныеЗаписи.НоменклатураПредставление + " в указанной партии не хватает товара. Реально есть " + ВыборкаДетальныеЗаписи.КоличествоОстаток;
				Сообщение.Сообщить();
				Отказ = Истина;
				Продолжить;
			КонецЕсли;
			Если Отказ Тогда
				Продолжить;
			КонецЕсли;
			// регистр ОстаткиНоменклатуры Расход
			Движение = Движения.ОстаткиНоменклатуры.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
			Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоДок;
			Себестоимость = ВыборкаДетальныеЗаписи.КоличествоДок / ВыборкаДетальныеЗаписи.КоличествоОстаток * ВыборкаДетальныеЗаписи.СуммаОстаток;
			Движение.Сумма = Себестоимость;
		КонецЕсли;
		// регистр Продажи 
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоДок;
		Движение.Себестоимость = Себестоимость;
		Движение.Продажа = ВыборкаДетальныеЗаписи.СуммаДок;
	КонецЦикла;
	

КонецПроцедуры
